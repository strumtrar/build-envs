IMAGE := "build-env-x86.debian.12.1-slim:latest"
USER_SRC := `whoami`
USER_DEST := "str"
ARCH := env_var('ARCH')
POD := env_var('PODMAN')
PODFLAGS := env_var('PODMANFLAGS')
PROJ := `if [[ -e .kernel ]]; then echo kernel; else echo barebox; fi`
HOOKS := "$PRJ_ROOT/justfiles/just-hooks"
ROOT := env_var('PRJ_ROOT')

alias b := build
alias c := compile
alias r := rsync

cmd := 'bash -i'

default: compile

@build:
    {{POD}} build -q -t {{IMAGE}} {{ROOT}}/build-envs/x86

_create-builddir:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    BRANCH=`cd $PWD && git symbolic-ref --short HEAD`
    BUILDDIR="/home/{{USER_SRC}}/work/build/$BRANCH"
    cd $PWD
    if [ ! -L build ]; then
        echo "Creating $BUILDDIR"
        mkdir -p $BUILDDIR
        echo "Creating $BUILDDIR link"
        ln -sf $BUILDDIR build
    fi    

_saveconfig: _create-builddir
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd $PWD
    if [[ ! -e build ]]; then
        exit 0
    fi
    if [[ ! -e build/.config ]]; then
        exit 0
    fi

    HASH=$(echo $(git describe --always))
    HASH=`echo $HASH | tr '[/]' '[_]'`

    if [[ $PWD =~ .*kernel*. ]]; then
        cp build/.config {{ROOT}}/config/kernelconfig-$HASH
        cd {{ROOT}}
        if [[ `git status --porcelain -- configs` ]]; then
                git add configs
                git commit -m "configs: update kernelconfig `date`"
        fi
    elif [[ $PWD =~ .*barebox*. ]]; then
        cp build/.config {{ROOT}}/config/bareboxconfig-$HASH
        cd {{ROOT}}
        if [[ `git status --porcelain -- configs` ]]; then
            git add configs
            git commit -m "configs: update bareboxconfig `date`"
        fi
    fi

@_connect flags="--rm" *command=cmd: build _create-builddir
    cd {{invocation_directory()}} && \
    {{POD}} run {{flags}} -it \
    {{PODFLAGS}} \
    -e ARCH=$ARCH \
    --workdir /{{ PROJ }} \
    --volume "$(pwd)"/build:/build \
    --volume "$(pwd)":/{{ PROJ }} \
    {{IMAGE}} \
    {{command}}

@connect *command=cmd:
    cd {{invocation_directory()}} && \
    just _connect --rm {{command}}

@objdump *parameters:
    cd {{invocation_directory()}} && \
    just connect ${CROSS_COMPILE}objdump -D {{parameters}}

pre-compile-hook:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        just -f $PRJ_ROOT/justfiles/just-hooks pre-kernel 
    elif [[ $PWD =~ .*barebox*. ]]; then
        just -f $PRJ_ROOT/justfiles/just-hooks pre-barebox
    fi

post-compile-hook:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        just -f {{HOOKS}} post-kernel
    elif [[ $PWD =~ .*barebox*. ]]; then
        just -f {{HOOKS}} post-barebox
    fi
    
post-rsync-hook:
    just -f {{HOOKS}} post-rsync

compile target="container" *parameters="": pre-compile-hook && _saveconfig post-compile-hook
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd $PWD
    if [[ {{target}} =~ .*container*. ]]; then
        just connect make O=/build -j$(nproc) {{parameters}}
        just connect make O=/build -j$(nproc) compile_commands.json
    else
        ARCH={{ARCH}} make O=build -j$(nproc) {{parameters}}
        ARCH={{ARCH}} make O=build -j$(nproc) compile_commands.json
    fi
    notify-send -i ~/cfg/x/builder.png "Compilation done"

rsync: compile
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$KERNELIMAGE $LG_PROXY:/home/str/work/customers/ptx/DistroKit/platform-x86_64/root/root/{{USER_DEST}}-linux-$HOSTNAME
        rsync -avz {{invocation_directory()}}/build/$KERNELIMAGE $LG_PROXY:/tftpboot/{{USER_DEST}}-linux-x86-com-e
    elif [[ $PWD =~ .*barebox*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$BAREBOXIMAGE $LG_PROXY:/tftpboot/{{USER_DEST}}-barebox-$HOSTNAME
    fi
