CACHE := "build-env-x86.debian.12.1-slim"
TAG := "latest"
USER_SRC := `whoami`
USER_DEST := "str"
ARCH := env_var('ARCH')
CROSS_COMPILE := env_var('CROSS_COMPILE')
CUSTOMER := env_var('CUSTOMER')
TOOLCHAIN := env_var('TOOLCHAIN')
TOOLCHAIN_VERSION := `echo $TOOLCHAIN | cut -d- -f2 | cut -d. -f1,2`
IMAGE := "build-env-arm-multi." + "oselas.toolchain-" + TOOLCHAIN_VERSION
ROOT := env_var('PRJ_ROOT')
POD := env_var('PODMAN')
PODFLAGS := env_var('PODMANFLAGS')
PROJ := `if [[ -e .kernel ]]; then echo kernel; else echo barebox; fi`
HOOKS := "$PRJ_ROOT/justfiles/just-hooks"

alias b := build
alias c := compile
alias r := rsync

cmd := 'bash -i'

default: compile

@pull:
    {{POD}} pull ghcr.io/strumtrar/{{CACHE}}:{{TAG}}

@build:
    {{POD}} build -t ghcr.io/strumtrar/{{CACHE}}:{{TAG}} --layers --build-arg=TOOLCHAIN={{TOOLCHAIN}} --cache-from ghcr.io/strumtrar/{{CACHE}} {{ROOT}}/build-envs/arm-multi

config-hook:
    #!/usr/bin/env bash
    set -x
    PWD="{{invocation_directory()}}"
    just -f {{CONFIG}} _saveconfig 

builddir-hook:
    #!/usr/bin/env bash
    set -x
    PWD="{{invocation_directory()}}"
    just -f {{CONFIG}} _create-builddir 

@_connect flags="--rm" *command=cmd: build builddir-hook
    cd {{invocation_directory()}} && \
    {{POD}} run {{flags}} -it \
    {{PODFLAGS}} \
    -e ARCH=$ARCH \
    -e CROSS_COMPILE=$CROSS_COMPILE \
    --workdir /{{ PROJ }} \
    --volume "$(pwd)"/build:/build \
    --volume "$(pwd)":/{{ PROJ }} \
    --net=host \
    {{IMAGE}} \
    {{command}}

@connect *command=cmd:
    cd {{invocation_directory()}} && \
    just _connect --rm {{command}}

@objdump *parameters:
    cd {{invocation_directory()}} && \
    just connect ${CROSS_COMPILE}objdump -D {{parameters}}

pre-compile-hook:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        just -f $PRJ_ROOT/justfiles/just-hooks pre-kernel
    elif [[ $PWD =~ .*barebox*. ]]; then
        just -f $PRJ_ROOT/justfiles/just-hooks pre-barebox
    fi

post-compile-hook:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        just -f {{HOOKS}} post-kernel
    elif [[ $PWD =~ .*barebox*. ]]; then
        just -f {{HOOKS}} post-barebox
    fi

post-rsync-hook:
    just -f {{HOOKS}} post-rsync

compile target="container" *parameters="": pre-compile-hook && config-hook post-compile-hook
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd $PWD
    if [[ {{target}} =~ .*container*. ]]; then
        just connect make O=/build -j$(nproc) {{parameters}}
        just connect make O=/build -j$(nproc) compile_commands.json
    else
        ARCH={{ARCH}} CROSS_COMPILE={{CROSS_COMPILE}} make O=build -j$(nproc) {{parameters}}
        ARCH={{ARCH}} CROSS_COMPILE={{CROSS_COMPILE}} make O=build -j$(nproc) compile_commands.json
    fi
    notify-send -i ~/cfg/x/builder.png "Compilation done"

copy: (compile "host")
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$KERNELIMAGE /tftpboot/{{USER_DEST}}-linux-$HOSTNAME
        if [[ ! -z $DEVICETREE ]]; then
            rsync -avz {{invocation_directory()}}/build/$DEVICETREE /tftpboot/{{USER_DEST}}-oftree-$HOSTNAME
        fi
    elif [[ $PWD =~ .*barebox*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$BAREBOXIMAGE /tftpboot/{{USER_DEST}}-barebox-$HOSTNAME
        if [ -e {{invocation_directory()}}/build/$XLOADIMAGE ]; then
            rsync -avz {{invocation_directory()}}/build/$XLOADIMAGE /tftpboot/{{USER_DEST}}-xload-$HOSTNAME
        fi
    fi

@dtcheck *parameters:
    cd {{invocation_directory()}} && \
    just connect make O=/build -j$(nproc) dt_binding_check DT_SCHEMA_FILES={{parameters}}
    just connect make O=/build -j$(nproc) dtbs_check DT_SCHEMA_FILES={{parameters}}

rsync: compile && post-rsync-hook
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"

    if [[ $MODE == "local" ]]; then
        REMOTE=""
    else
        REMOTE="$LG_PROXY:"
    fi
    if [[ $PWD =~ .*kernel*. ]]; then
        rsync -az {{invocation_directory()}}/build/$KERNELIMAGE $REMOTE/tftpboot/{{USER_DEST}}-linux-$HOSTNAME && \
        notify-send -i ~/cfg/x/bomber.png "Sent $REMOTE:{{USER_DEST}}-linux-$HOSTNAME!"
        if [[ ! -z $DEVICETREE && ! -d $DEVICETREE ]]; then
            rsync -az {{invocation_directory()}}/build/$DEVICETREE $REMOTE/tftpboot/{{USER_DEST}}-oftree-$HOSTNAME
            notify-send -i ~/cfg/x/bomber.png "Sent $REMOTE:{{USER_DEST}}-oftree-$HOSTNAME!"
        fi
    elif [[ $PWD =~ .*barebox*. ]]; then
        if [ -f {{invocation_directory()}}/build/$BAREBOXIMAGE ]; then
            rsync -avz {{invocation_directory()}}/build/$BAREBOXIMAGE $REMOTE/tftpboot/{{USER_DEST}}-barebox-$HOSTNAME
            notify-send -i ~/cfg/x/bomber.png "Sent $REMOTE:{{USER_DEST}}-barebox-$HOSTNAME!"
        fi
        if [ -f {{invocation_directory()}}/build/$XLOADIMAGE ]; then
            rsync -avz {{invocation_directory()}}/build/$XLOADIMAGE $REMOTE/tftpboot/{{USER_DEST}}-xload-$HOSTNAME
            notify-send -i ~/cfg/x/bomber.png "Sent $REMOTE:{{USER_DEST}}-xload-$HOSTNAME!"
        fi
    fi
