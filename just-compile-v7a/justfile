USER_SRC := `whoami`
USER_DEST := "str"
ARCH := env_var('ARCH')
CROSS_COMPILE := env_var('CROSS_COMPILE')
CUSTOMER := env_var('CUSTOMER')
TOOLCHAIN := env_var('TOOLCHAIN')
TOOLCHAIN_VERSION := `echo $TOOLCHAIN | cut -d- -f2 | cut -d. -f1,2`
IMAGE := "build-env-arm-multi:" + "OSELAS.Toolchain-" + TOOLCHAIN_VERSION
ROOT := env_var('ROOT')
POD := env_var('PODMAN')
PODFLAGS := env_var('PODMANFLAGS')
PROJ := `if [[ -e .kernel ]]; then echo kernel; else echo barebox; fi`

alias b := build
alias c := compile
alias r := rsync
alias t := test
alias l := lg

cmd := 'bash -i'

default: compile

@build:
    {{POD}} build -q -t {{IMAGE}} --build-arg=TOOLCHAIN={{TOOLCHAIN}} {{ROOT}}/build-envs/arm-multi

tag +task:
    {{POD}} image tag {{IMAGE}} {{CUSTOMER}}:{{task}}

done *tag:
    {{POD}} save --compress {{ROOT}}_{{tag}} {{IMAGE}}

ps:
    {{POD}} ps --all --storage

rm +image:
    {{POD}} rm {{image}}

_con:
    labgrid-client con --logfile {{ROOT}}/logs/`date +"%Y_%m_%d_%I_%M"`.log

_lock:
    #!/usr/bin/env bash
    set -x
    if [[ -n $LG_PLACE ]]; then
        labgrid-client who | grep $LG_PLACE
        if [ $? != 0 ]; then
            labgrid-client lock
        fi
    fi

_create-builddir:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    BRANCH=`cd $PWD && git symbolic-ref --short HEAD`
    BUILDDIR="/home/{{USER_SRC}}/work/build/$BRANCH"
    cd $PWD
    if [ ! -L build ]; then
        echo "Creating $BUILDDIR"
        mkdir -p $BUILDDIR
        echo "Creating $BUILDDIR link"
        ln -sf $BUILDDIR build
    fi    

_saveconfig: _create-builddir
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd $PWD
    if [[ ! -e build ]]; then
        exit 0
    fi
    if [[ ! -e build/.config ]]; then
        exit 0
    fi
    if [[ $PWD =~ .*kernel*. ]]; then 
        cp build/.config {{ROOT}}/configs/kernelconfig
    elif [[ $PWD =~ .*barebox*. ]]; then
        cp build/.config {{ROOT}}/configs/bareboxconfig
    fi

_unlock:
    labgrid-client unlock

_pw +dir="on":
    labgrid-client pw {{dir}}

_pwcyc:
    labgrid-client pw cycle

_create-builddir:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    BRANCH=`cd $PWD && git symbolic-ref --short HEAD`
    BUILDDIR="/home/{{USER_SRC}}/work/build/$BRANCH"
    cd $PWD
    if [ ! -L build ]; then
        echo "Creating $BUILDDIR"
        mkdir -p $BUILDDIR
        echo "Creating $BUILDDIR link"
        ln -sf $BUILDDIR build
    fi    

@_connect flags="--rm" *command=cmd: build _create-builddir
    cd {{invocation_directory()}} && \
    {{POD}} run {{flags}} -it \
    {{PODFLAGS}} \
    -e ARCH=$ARCH \
    -e CROSS_COMPILE=$CROSS_COMPILE \
    --workdir /{{ PROJ }} \
    --volume "$(pwd)"/build:/build \
    --volume "$(pwd)":/{{ PROJ }} \
    {{IMAGE}} \
    {{command}}

@connect *command=cmd:
    cd {{invocation_directory()}} && \
    just _connect --rm {{command}}

@objdump *parameters:
    cd {{invocation_directory()}} && \
    just connect ${CROSS_COMPILE}objdump -D {{parameters}}

@freeze +task: (_saveconfig)
    just _connect "" make O=/build -j$(nproc)
    just tag {{task}}

@compile target="container" *parameters="": (_saveconfig)
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd $PWD
    if [[ {{target}} =~ .*container*. ]]; then
        just connect make O=/build -j$(nproc) {{parameters}}
        just connect make O=/build -j$(nproc) compile_commands.json
    else
        ARCH={{ARCH}} CROSS_COMPILE={{CROSS_COMPILE}} make O=build -j$(nproc) {{parameters}}
        ARCH={{ARCH}} CROSS_COMPILE={{CROSS_COMPILE}} make O=build -j$(nproc) compile_commands.json
    fi

copy: (compile "host")
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$KERNELIMAGE /tftpboot/{{USER_DEST}}-linux-$HOSTNAME
        if [[ ! -z $DEVICETREE ]]; then
            rsync -avz {{invocation_directory()}}/build/$DEVICETREE /tftpboot/{{USER_DEST}}-oftree-$HOSTNAME
        fi
    elif [[ $PWD =~ .*barebox*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$BAREBOXIMAGE /tftpboot/{{USER_DEST}}-barebox-$HOSTNAME
        if [ -e {{invocation_directory()}}/build/$XLOADIMAGE ]; then
            rsync -avz {{invocation_directory()}}/build/$XLOADIMAGE /tftpboot/{{USER_DEST}}-xload-$HOSTNAME
        fi
    fi

@dtcheck *parameters:
    cd {{invocation_directory()}} && \
    just connect make O=/build -j$(nproc) dt_binding_check DT_SCHEMA_FILES={{parameters}}
    just connect make O=/build -j$(nproc) dtbs_check DT_SCHEMA_FILES={{parameters}}

@boot_bin: compile
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    cd bosch_be && mkimage -T zynqmpbif -d boot.bif boot.bin

rsync: compile
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $PWD =~ .*kernel*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$KERNELIMAGE /srv/tftp/{{USER_DEST}}-linux-$HOSTNAME && \
        if [[ ! -z $DEVICETREE ]]; then
            rsync -avz {{invocation_directory()}}/build/$DEVICETREE /srv/tftp/{{USER_DEST}}-oftree-$HOSTNAME
        fi
    elif [[ $PWD =~ .*barebox*. ]]; then
        rsync -avz {{invocation_directory()}}/build/$BAREBOXIMAGE ~/work/customers/bosch.be.ppm4/petalinux-docker/zcu102/images/linux/barebox-zynqmp-ppm4.img
    fi

restart: rsync _pwcyc

test: rsync _lock _pw _con && (_pw "off") _unlock

lg: _lock _pw _con && (_pw "off") _unlock
