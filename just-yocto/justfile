USER_SRC := `whoami`
USER_DEST := "str"
BASEIMAGE := env_var('BASEIMAGE')
CACHE := "yocto-debian-" + BASEIMAGE + "-build-env"
TAG := "latest"
MACHINE := `echo $MACHINE`
DISTRO := env_var('DISTRO')
ROOT := env_var('PRJ_ROOT')
POD := env_var('PODMAN')
PODFLAGS := env_var('PODMANFLAGS')

alias b := build
alias c := compile-bsp

cmd := 'bash -i'

@pull:
    {{POD}} pull ghcr.io/strumtrar/{{CACHE}}:{{TAG}}

@build:
    {{POD}} build -q -t ghcr.io/strumtrar/{{CACHE}}:{{TAG}} --layers --cache-from ghcr.io/strumtrar/{{CACHE}} $PRJ_ROOT/build-envs/yocto-build-env

_connect machine *command=cmd: pull
    cd {{invocation_directory()}} && \
    podman run -it --rm --userns=keep-id \
    -e MACHINE={{machine}} \
    -e DISTRO={{DISTRO}} \
    --network host \
    --workdir /bsp \
    --volume "$(pwd)":/bsp \
    --volume ~/repos:/repos \
    --volume ~/work/yocto/dl-dir:/bsp/build/downloads \
    --volume ~/work/yocto/sstate-cache:/bsp/build/sstate-cache \
    {{CACHE}}:{{TAG}} \
    {{command}}

_init_build_script:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    BUILD_SCRIPT="build-{{MACHINE}}.sh"
    cd $PWD
    if [ ! -d build ]; then
        mkdir build
    fi
    if [ ! -f $BUILD_SCRIPT ]; then
        echo "#!/bin/bash" > $BUILD_SCRIPT
        echo ". ./oe-init-build-env build" >> $BUILD_SCRIPT
        echo "bitbake \$@" >> $BUILD_SCRIPT
        chmod +x $BUILD_SCRIPT
        if [ ! -d build/conf ]; then
            mkdir -p build/conf
        fi
        if [ ! -e build/conf/auto.conf ]; then
            echo "BB_FETCH_PREMIRRORONLY = \"0\"" > build/conf/auto.conf
            echo "BB_NO_NETWORK = \"0\"" >> build/conf/auto.conf
            echo "BB_ALLOWED_NETWORKS = \"\"" >> build/conf/auto.conf
            if [ -e $PWD/../.auto.conf ]; then
                cat $PWD/../.auto.conf >> build/conf/auto.conf
            fi
        fi
    fi

_init_devtool_script:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    DEVTOOL_SCRIPT="devtool-{{MACHINE}}.sh"
    cd $PWD
    if [ ! -f $DEVTOOL_SCRIPT ]; then
        echo "#!/bin/bash" > $DEVTOOL_SCRIPT
        echo ". ./oe-init-build-env build" >> $DEVTOOL_SCRIPT
        echo "devtool \$@" >> $DEVTOOL_SCRIPT
        chmod +x $DEVTOOL_SCRIPT
    fi

_init_wic_script:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    WIC_SCRIPT="wic-{{MACHINE}}.sh"
    cd $PWD
    if [ ! -f $WIC_SCRIPT ]; then
        echo "#!/bin/bash" > $WIC_SCRIPT
        echo ". ./oe-init-build-env build" >> $WIC_SCRIPT
        echo "wic \$@" >> $WIC_SCRIPT
        chmod +x $WIC_SCRIPT
    fi

_init_run_native:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    OE_SCRIPT="oe-run-native-{{MACHINE}}.sh"
    cd $PWD
    if [ ! -f $OE_SCRIPT ]; then
        echo "#!/bin/bash" > $OE_SCRIPT
        echo ". ./oe-init-build-env build" >> $OE_SCRIPT
        echo "oe-run-native \$@" >> $OE_SCRIPT
        chmod +x $OE_SCRIPT
    fi

compile-bsp +target: _init_build_script
    cd {{invocation_directory()}} && \
    just _connect {{MACHINE}} /bsp/build-{{MACHINE}}.sh {{target}}
    notify-send -i ~/cfg/x/builder.png "{{MACHINE}} :: {{target}} done"

devtool +target: _init_devtool_script
    cd {{invocation_directory()}} && \
    just _connect {{MACHINE}} /bsp/devtool-{{MACHINE}}.sh {{target}}

wic +target: _init_wic_script
    cd {{invocation_directory()}} && \
    just _connect {{MACHINE}} /bsp/wic-{{MACHINE}}.sh {{target}}

rauc target file: _init_run_native
    cd {{invocation_directory()}} && \
    just _connect {{MACHINE}} /bsp/oe-run-native-{{MACHINE}}.sh rauc-native rauc --no-verify {{target}} {{file}}

rsync:
    #!/usr/bin/env bash
    PWD="{{invocation_directory()}}"
    if [[ $MODE == "local" ]]; then REMOTE=""; else REMOTE="$LG_PROXY:"; fi
    rsync -avz {{invocation_directory()}}/build/tmp/deploy/images/ppm4/BOOT.BIN $REMOTE/tftpboot/{{USER_DEST}}-bootbin-$HOSTNAME
    rsync -vz --copy-links {{invocation_directory()}}/build/tmp/deploy/images/ppm4/Image $REMOTE/tftpboot/{{USER_DEST}}-linux-$HOSTNAME
    rsync -vz --copy-links {{invocation_directory()}}/build/tmp/deploy/images/ppm4/zynqmp-ppm4.dtb $REMOTE/tftpboot/{{USER_DEST}}-oftree-$HOSTNAME
    rsync -vz --copy-links {{invocation_directory()}}/build/tmp/deploy/images/ppm4/msd-base-image-ppm4.wic.gz $REMOTE/tftpboot/{{USER_DEST}}-wic-$HOSTNAME
    rsync -vz --copy-links {{invocation_directory()}}/build/tmp/deploy/images/ppm4/msd-base-bundle-ppm4.raucb $REMOTE/tftpboot/{{USER_DEST}}-raucb-$HOSTNAME
